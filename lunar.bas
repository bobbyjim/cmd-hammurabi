10 	SC = 32 :REM SCREEN INDENT
20 	? CHR$(147) "\X11"
30 	? TAB(SC) "LUNAR"
40 	? TAB(SC/2) "\X11\X11CREATIVE COMPUTING   MORRISTOWN, NEW JERSEY"
50 	? "\X11\X11\X11\X11THIS IS A COMPUTER SIMULATION OF AN APOLLO LUNAR"
60 	? "LANDING CAPSULE."
70 	? "\X11\X11\X11THE ON-BOARD COMPUTER HAS FAILED (IT WAS MADE BY"
80 	? "XEROX) SO YOU HAVE TO LAND THE CAPSULE MANUALLY.\X11"
90 	? "SET BURN RATE OF RETRO ROCKETS TO ANY VALUE BETWEEN"
100 	? "0 (FREE FALL) AND 200 (MAXIMUM BURN) POUNDS PER SECOND."
110 	? "SET NEW BURN RATE EVERY 10 SECONDS."
120 	? "\X11CAPSULE WEIGHT 32,500 LBS; FUEL WEIGHT 16,500 LBS."
130 	? "\X11\X11\X11GOOD LUCK"
140 	L%=0 :REM SECONDS ELAPSED
150 	? "\X11 SEC", " KM", " KPH", "LB FUEL", "BURN RATE\X11"
160 	A=120 :V=1 :M=33000 :N=16500 :G=.001 :Z=1.8
170 	? L%, INT(1.6*A), INT(1.6*3600*V),M-N,:INPUT K :T=10
180 	IF M-N<.001 GOTO 260
190 	IF T<.001   GOTO 170
200 	S=T :IF M>=N+S*K GOTO 220
210 	S=(M-N)/K
220 	GOSUB 480 :IF I<=0 GOTO 390
230 	IF V<=0 GOTO 250	
240 	IF J<0 GOTO 420
250 	GOSUB 380 :GOTO 180
260 	? "\X11FUEL OUT AT" L% "SECONDS" :S=(-V+SQR(V*V+2*A*G))/G
270 	V=V+G*S :L%=L%+S
280 	W=1.6*3600*V :? "\X11ON MOON AT" L% "SECONDS - IMPACT VELOCITY" INT(W) "KPH"
290 	IF W<=1.2 THEN ? "PERFECT LANDING!" :GOTO 510
300 	IF W<=10  THEN ? "GOOD LANDING (COULD BE BETTER)" :GOTO 510
310 	IF W>60 THEN 350
320 	? "\X11CRAFT DAMAGE... YOU'RE STRANDED HERE UNTIL A RESCUE"
330 	? "PARTY ARRIVES.  HOPE YOU HAVE ENOUGH OXYGEN!"
340 	GOTO 510
350 	? "\X11SORRY THERE WERE NO SURVIVORS.  YOU BLEW IT!"
360 	? "IN FACT, YOU BLASTED A NEW LUNAR CRATER" W*.052768 "METERS DEEP!"
370 	GOTO 510
380 	L%=L%+S :T=T-S :M=M-S*K :A=I :V=J :RETURN
390 	IF S<.005 GOTO 280
400 	D = V + SQR(V*V+2*A*ABS(G-Z*K/M)) :S=2*A/D
410 	GOSUB 480 :GOSUB 380 :GOTO 390
420 	W=(1-M*G/(Z*K))/2 :S=M*V/(Z*K*(W+SQR(W*W+V/Z)))+0.5 :GOSUB 480
430 	REM W=(1-M*G/(Z*K))/2 :S=M*V/(Z*K*(W+SQR(W*Q+V/Z)))+0.5 :GOSUB 480
440 	IF I<=0 GOTO 390
450 	GOSUB 380 :IF J>0 GOTO 180
460 	IF V>0 GOTO 420
470 	GOTO 180
480 	Q=S*K/M :J=V+G*S+Z*(-Q-Q*Q/2-Q^3/3-Q^4/4-Q^5/5)
490 	I=A-G*S*S/2 - V*S + Z*Z*(Q/2 + Q*Q/6 + Q^3/12 + Q^4/20 + Q^5/30)
500 	RETURN
510 	? "\X11\X11\X11TRY AGAIN??\X11" :GOTO 90
